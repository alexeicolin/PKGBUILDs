# ArmSoc (OMAP + Mali)  FrameBuffer driver for Xorg from Linaro.
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Erik Andersson <erik.h.andersson@gmail.com>

buildarch=4

pkgname=xf86-video-armsoc-git
#_commit=f16b5c842ffd6f80a3091fc893bc2de8162bc49b
_commit=459fcb44e1782facfc7954a7e276e1492a3f312b
pkgver=234.459fcb4
pkgrel=1
epoch=1
arch=('armv7h')
url="http://cgit.freedesktop.org/xorg/driver/xf86-video-armsoc"
license=('GPL2')
makedepends=('git' 'pkgconfig' 'xorg-server-devel' 'resourceproto' 'scrnsaverproto')
options=('!libtool')
conflicts=(xf86-video-armsoc)
provides=(xf86-video-armsoc)
source=("$pkgname::git://anongit.freedesktop.org/xorg/driver/xf86-video-armsoc#commit=${_commit}"
	'0001-armsoc_driver-do-not-Attend-Ignore-gone-clients.patch')
md5sums=('SKIP'
	'c6c6a3d5c23eb82685f13306644e0171')

prepare() {
  cd $pkgname
  patch -p1 < ${startdir}/0001-armsoc_driver-do-not-Attend-Ignore-gone-clients.patch
}

pkgver() {
  cd $pkgname
  echo $(git rev-list --count makepkg).$(git rev-parse --short makepkg)
}

build() {
  cd $pkgname

  CFLAGS=`echo $CFLAGS | sed -e 's/vfpv3-d16/neon/'` && CXXFLAGS="$CFLAGS"
  ./autogen.sh --prefix=/usr --with-drmmode=exynos
  make
}

package() {
  cd $pkgname

  make DESTDIR="${pkgdir}" install
}

